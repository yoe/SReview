#!/usr/bin/perl -w

use v5.28;

use strict;
use warnings;

use DBI;
use File::Basename;
use File::Copy;
use File::Temp qw/tempdir/;
use Media::Convert::Asset;
use Media::Convert::Asset::Concat;
use Media::Convert::AvSync;
use Media::Convert::KeyframeFinder;
use Media::Convert::Map;
use Media::Convert::Normalizer;;
use Media::Convert::Pipe;;
use SReview::Config::Common;
use SReview::Files::Factory;
use SReview::Talk;
use JSON::MaybeXS;

=head1 NAME

sreview-cut - cut a talk out of the raw recording data

=head1 SYNOPSIS

sreview-cut TALKID

=head1 DESCRIPTION

C<sreview-cut> performs the following actions:

=over

=item *

Look up the talk with id TALKID in the database

=item *

From the raw files, extract the amount of video that, according to the
schedule, is part of the talk of which the event was given as the main
video (with adjustments as specified by the reviewer, if any, applied).

=item *

Extract the 20 minutes just before and the 20 minuts just after the main
video into the pre and post videos.

=item *

Apply A/V sync correction values if any exist.

=item *

Perform audio normalization, if enabled in the configuration

=item *

Extract sample videos from all three channels into preview audio
streams, if enabled in the configuration

=item *

Move the talk to the next state.

=back

sreview-cut will B<never> re-encode the original video, and will
re-encode the original audio of the main video at most once, after audio
normalization if that was enabled in the configuration.

Any re-transcodes should be performed by C<sreview-transcode> (for
production) and/or C<sreview-previews> (for review previews)

=head1 CONFIGURATION

C<sreview-cut> considers the following configuration values:

=over

=cut

my $config = SReview::Config::Common::setup;

my %duration_args;

if(defined($config->get('canonical_duration'))) {
        $duration_args{canonical_duration} = $config->get('canonical_duration');
}

$ENV{TMPDIR} = $config->get("workdir");

=item workdir

The location where any temporary files are stored. Defaults to C</tmp>,
but can be overridden if necessary. These temporary files are removed
when C<sreview-cut> finishes.

=cut

my $tempdir = tempdir("cutXXXXXX", DIR => $config->get("workdir"), CLEANUP => 1);

=item dbistring

The DBI string used to connect to the database.

=cut

my $talkid = $ARGV[0];

=item audio_multiplex_mode

The way in which the primary and backup audio channels are encoded in
the video. Can be one of:

=over

=item stereo

The primary audio is in the left channel, and the backup audio is in the
right channel.

=item stream

The primary audio is in the first audio stream (a mono stream), and the
backup audio is in the second audio stream (also a mono stream).

=item none

There is only one audio stream, and if it is a stereo channel then the
right channel is not the backup audio.

=back

=cut

my $multiplex_mode = $config->get("audio_multiplex_mode");
my $maptype;
my $primary_audio;
my $backup_audio;
my $both_audio;

if($multiplex_mode eq "stereo") {
	$maptype = "channel";
	$primary_audio = "left";
	$backup_audio = "right";
	$both_audio = "both";
} elsif($multiplex_mode eq "stream") {
	$maptype = "astream";
	$primary_audio = "0";
	$backup_audio = "1";
	$both_audio = "-1";
} else {
	$maptype = "none";
	$primary_audio = "both";
	$backup_audio = "both";
	$both_audio = "both";
}

my $dbh = DBI->connect($config->get("dbistring"));

$dbh->begin_work;

my $started = $dbh->prepare("UPDATE talks SET progress='running' WHERE id = ? AND state = 'cutting'");
$started->execute($talkid);

$dbh->commit;

my $talk = SReview::Talk->new(talkid => $talkid);

if($talk->get_flag("is_injected")) {
		$dbh->prepare("UPDATE talks SET progress='broken', comments='tried to re-cut an injected talk' WHERE id = ?")->execute($talkid);
		exit;
}

my $corrections = $talk->corrections;

my @segments_pre;
my @segments_main;
my @segments_post;

my $prelen = 0;
my $mainlen = 0;
my $postlen = 0;

=item inputglob

The location of the (raw) input files. These files should have been put
into the database by way of C<sreview-detect>

=item pubdir

The location of the intermediate files that are published to reviewers.

=item accessmethods

The L<SReview::Files> implementation used to access the input files
and/or the intermediate files.

=cut

my $input_coll = SReview::Files::Factory->create("input", $config->get("inputglob"));
my $output_coll = SReview::Files::Factory->create("intermediate", $config->get("pubdir"));

$output_coll->delete_files(relnames => [dirname($talk->relative_name)]);

my $fragments_title = encode_json($talk->avs_video_fragments);

say "Video fragments:";
say "================";

say $fragments_title;
say "-" x length($fragments_title);

FRAGMENT:
foreach my $row(@{$talk->avs_video_fragments}) {
        say "Considering work on " . $row->{raw_filename};
	if($row->{raw_length_corrected} <= 0.5 && $row->{talkid} < 0) {
                say "skipping, file length less than half a second";
                next FRAGMENT;
        }
	my $start;
	my $stop;
	my $target;
	my $segments;

	if($row->{talkid} == -1) {
		$target = "pre";
		$segments = \@segments_pre;
		$prelen += $row->{raw_length_corrected};
	} elsif($row->{talkid} == -2) {
		$target = "post";
		$segments = \@segments_post;
		$postlen += $row->{raw_length_corrected};
	} else {
		$target = "main";
		$segments = \@segments_main;
		$mainlen += $row->{raw_length_corrected};
	}
	my $input_file = $input_coll->get_file(relname => $row->{raw_filename});
	my $input = Media::Convert::Asset->new(url => $input_file->filename, %duration_args);
        my $kfs = Media::Convert::KeyframeFinder->new(asset => $input);
	my $output = Media::Convert::Asset->new(url => "$tempdir/$target" . $row->{rawid} . ".mkv", %duration_args);
        my $target_length = $row->{raw_length_corrected};
        my $frame_length = 0;
	if($row->{fragment_start} != 0) {
                say "The wanted fragment does not start at the beginning, considering what to do";
                my $prev_kf = 0;
                my $cur_kf = 0;
                KF:
                foreach my $kf(sort {$a <=> $b} @{$kfs->keyframes}) {
                        $cur_kf = $kf;
                        last KF if($kf >= $row->{fragment_start});
                        $prev_kf = $kf;
                }
                if($prev_kf != $row->{fragment_start}) {
                        say "The desired cut point is not on a key frame, deciding how to handle the situation";
			my $profile;
			if(defined($config->get("input_profile"))) {
                                $profile = Media::Convert::Asset::ProfileFactory->create($config->get("input_profile"), $input, $config->get("extra_profiles"));
			} else {
			        $profile = $input;
			}
                        my $rate = $profile->video_framerate;
                        my ($numerator, $denumerator) = split /\//, $rate;
                        $frame_length = 1 / ($numerator / $denumerator);
                        if (($cur_kf - $row->{fragment_start}) <= ($frame_length)) {
                                # Cut point is less than one frame before the
                                # next key frame. Move the cut point to the key
                                # frame.
                                say "The desired cut point is less than one frame before the next key frame. Moving the cut point to the key frame.";
                                $output->fragment_start($cur_kf);
                        } else {
                                say "The desired cut point does not fall on or near a key frame. Creating a new key frame at the desired cut point.";
                                # The start point is reasonably far away from a
                                # key frame. This means we need to create one,
                                # otherwise the video does not start correctly,
                                # and this confuses firefox (amongst others).
                                #
                                # Handle this using the following algorithm:
                                # - First, extract the bit from th last key frame
                                #   before our start point to the next key frame after
                                #   our start point, and re-encode it with a forced key
                                #   frame at our start point.
                                # - Next, extract the bit from our start point to the
                                #   end of the newly encoded video from that video.
                                # - Copy the rest of the video file from the next key
                                #   frame without re-encoding.
                                my $temp = Media::Convert::Asset->new(url => "$tempdir/kftemp" . $row->{rawid} . ".mkv", %duration_args, reference => $profile);
                                $temp->fragment_start($prev_kf);
                                $temp->duration($cur_kf - $prev_kf);
                                $temp->force_key_frames($row->{fragment_start} - $prev_kf);
                                Media::Convert::Pipe->new(inputs => [$input], "map" => [Media::Convert::Map->new(input => $input, type => "allcopy")], output => $temp, vcopy => 0, acopy => 1)->run();
                                my $recode_fragment = Media::Convert::Asset->new(url => $temp->url, %duration_args);
                                my $add_fragment = Media::Convert::Asset->new(url => "$tempdir/recoded_$target" . $row->{rawid} . ".mkv", %duration_args, fragment_start => $row->{fragment_start} - $prev_kf);
                                Media::Convert::Pipe->new(inputs => [$recode_fragment], "map" => [Media::Convert::Map->new(input => $recode_fragment, type => "allcopy")], output => $add_fragment, vcopy => 1, acopy => 1)->run();
                                push @$segments, Media::Convert::Asset->new(url => $add_fragment->url, %duration_args);
                                $output->fragment_start($cur_kf);
                                $target_length -= $temp->duration;
                        }
                } else {
                        say "The desired cut point is on a key frame. Doing a dumb copy.";
		        $output->fragment_start($row->{fragment_start});
                }
	} else {
                say "The wanted fragment starts at the beginning of the file. Doing a dumb copy.";
        }
	if($row->{raw_length} ne $row->{raw_length_corrected}) {
                say "The desired duration is $target_length";
                if($target_length < $frame_length) {
                        say "This is less than a frame, skipping this fragment.";
                        next FRAGMENT;
                }
		$output->duration($target_length);
	}
	Media::Convert::Pipe->new(inputs => [$input], "map" => [Media::Convert::Map->new(input => $input, type => "allcopy")], output => $output, vcopy => 1, acopy => 1)->run();
	push @$segments, Media::Convert::Asset->new(url => $output->url, %duration_args);
}

my $pre = undef;
my $main = undef;
my $post = undef;

if(scalar(@segments_pre)>0) {
	$pre = Media::Convert::Asset::Concat->new(url => "$tempdir/pre.txt", components => \@segments_pre, input_params => {fflags => "+nofillin"});
}
$main = Media::Convert::Asset::Concat->new(url => "$tempdir/main.txt", components => \@segments_main, input_params => {fflags => "+nofillin"});
if(scalar(@segments_post)>0) {
	$post = Media::Convert::Asset::Concat->new(url => "$tempdir/post.txt", components => \@segments_post, input_params => {fflags => "+nofillin"});
}

my $pre_new = undef;
my $main_new = Media::Convert::Asset->new(url => "$tempdir/main.mkv", %duration_args);
my $post_new = undef;
my @videos;

if(defined($pre)) {
        say "Concatenating pre fragments";
	$pre_new = Media::Convert::Asset->new(url => "$tempdir/pre.mkv", %duration_args);
	Media::Convert::Pipe->new(inputs => [$pre], output => $pre_new, map => [Media::Convert::Map->new(input => $pre, type => "allcopy")], vcopy => 1, acopy => 1)->run();
	push @videos, $pre_new;
}
say "Concatenating main fragments";
Media::Convert::Pipe->new(inputs => [$main], output => $main_new, map => [Media::Convert::Map->new(input => $main, type => "allcopy")], vcopy => 1, acopy => 1)->run();
push @videos, $main;
if(defined($post)) {
        say "Concatenating post fragments";
	$post_new = Media::Convert::Asset->new(url => "$tempdir/post.mkv", %duration_args);
	Media::Convert::Pipe->new(inputs => [$post], output => $post_new, map => [Media::Convert::Map->new(input => $post, type => "allcopy")], vcopy => 1, acopy => 1)->run();
	push @videos, $post_new;
}

$pre = $pre_new;
$post = $post_new;
$main = $main_new;

sub reload {
	@videos = ();
	if(defined($pre)) {
		$pre = Media::Convert::Asset->new(url => $pre->url, %duration_args);
		push @videos, $pre;
	}
	$main = Media::Convert::Asset->new(url => $main->url, %duration_args);
	push @videos, $main;
	if(defined($post)) {
		$post = Media::Convert::Asset->new(url => $post->url, %duration_args);
		push @videos, $post;
	}
}

reload();

say "Extracting audio samples";
my $samplestart = ($main->duration / 2) - 30;
my $samplelen = 60;
$samplestart = ($samplestart > 0) ? $samplestart : 0;
$samplelen = ($samplelen > $mainlen) ? $mainlen : $samplelen;
my @choices = ($primary_audio, $backup_audio, $both_audio);
foreach my $stream(0, 1, 2) {
        say "stream $stream wav";
	my $sample_wav_file = $output_coll->add_file(relname => $talk->relative_name . "/audio$stream.wav");
	my $sample_wav = Media::Convert::Asset->new(url => $sample_wav_file->filename, fragment_start => $samplestart, duration => $samplelen, %duration_args);
	Media::Convert::Pipe->new(inputs => [$main], output => $sample_wav, map => [Media::Convert::Map->new(input => $main, type => $maptype, choice => $choices[$stream])], acopy => 0, vskip => 1)->run();
	$sample_wav_file->store_file;
	foreach my $codec(qw/mp3 ogg/) {
                say "stream $stream $codec";
		my $tmp = $output_coll->add_file(relname => $talk->relative_name . "/audio$stream.$codec");
		Media::Convert::Pipe->new(inputs => [$sample_wav], output => Media::Convert::Asset->new(url => $tmp->filename, %duration_args), acopy => 0, vskip => 1)->run();
		$tmp->store_file;
	}
}

if($corrections->{offset_audio} != 0) {
        say "Performing A/V sync correction";
	for my $vid(@videos) {
		my $tmp = Media::Convert::Asset->new(url => "$tempdir/temp.mkv", %duration_args);
		Media::Convert::AvSync->new(input => $vid, output => $tmp, audio_delay => $corrections->{offset_audio})->run();
		move($tmp->url, $vid->url);
	}
	reload();
}

if(!$talk->get_flag("keep_audio")) {
        say "Performing audio normalization";
	my $tmp = Media::Convert::Asset->new(url => "$tempdir/channel.mkv", audio_codec => $main->audio_codec, %duration_args);
	Media::Convert::Pipe->new(inputs => [$main], output => $tmp, map => [Media::Convert::Map->new(input => $main, type => $maptype, choice => $choices[$corrections->{audio_channel}])], acopy => 0, vskip => 1)->run();
	my $tmp2 = Media::Convert::Asset->new(url => "$tempdir/merged.mkv", %duration_args);
	Media::Convert::Pipe->new(inputs => [$tmp, $main], output => $tmp2, map => [Media::Convert::Map->new(input => $main, type => "stream", choice => "video"), Media::Convert::Map->new(input => $tmp, type => "stream", choice => "audio")])->run();
	move($tmp2->url, $main->url);
	reload();
	eval {
		Media::Convert::Normalizer->new(input => $main, output => Media::Convert::Asset->new(url => "$tempdir/temp.mkv", %duration_args))->run();
	};
	if($@) {
		say "normalization failed, skipping...";
	} else {
		my @stat = stat("$tempdir/temp.mkv");
		if($stat[7] != 0) {
			move("$tempdir/temp.mkv", $main->url);
			reload();
		}
	}
}

say "Done, updating database and uploading files if necessary";
my $postlen_db = 0;
my $prelen_db = 0;
if(defined($pre)) {
	$prelen_db = $pre->duration;
	my $pre_file = $output_coll->add_file(relname => $talk->relative_name . "/pre.mkv");
	move($pre->url, $pre_file->filename);
	$pre_file->store_file;
} else {
	$output_coll->delete_files(relnames => [$talk->relative_name . "/pre.mkv"]);
}
if(defined($post)) {
	$postlen_db = $post->duration;
	my $post_file = $output_coll->add_file(relname => $talk->relative_name . "/post.mkv");
	move($post->url, $post_file->filename);
	$post_file->store_file;
} else {
	$output_coll->delete_files(relnames => [$talk->relative_name . "/post.mkv"]);
}

my $final = $output_coll->add_file(relname => $talk->relative_name . "/main.mkv");

move($main->url, $final->filename);

$final->store_file;

$dbh->begin_work;

my $update = $dbh->prepare("UPDATE talks SET progress='done', prelen = ?::interval, postlen = ?::interval WHERE id = ? AND state='cutting'");
$update->execute("$prelen_db seconds", "$postlen_db seconds", $talkid);

$dbh->commit;

=back

=head1 SEE ALSO

L<sreview-transcode>, L<sreview-previews>, L<sreview-skip>, L<sreview-config>

=cut
